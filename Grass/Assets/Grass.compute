//#include "UnityCG.cginc"


// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel Main
#pragma multi_compile_local __ DISTANCE_CULL_ENABLED


struct GrassBlade {

    float3 position;
    //float2 facing;
    float rotAngle;
    float hash;
    float height;
    float width;
    float tilt;
    float bend;
    float3 surfaceNorm;
    float3 color;
    float windForce;
    float sideBend;
    float clumpColorDistanceFade;
};


struct ClumpParametersStruct {
float pullToCentre;
float pointInSameDirection;
float baseHeight;
float heightRandom;
float baseWidth;
float widthRandom;
float baseTilt;
float tiltRandom;
float baseBend;
float bendRandom;
};


//uniform Texture2D<float4> _CameraDepthTexture;



StructuredBuffer<ClumpParametersStruct> _ClumpParameters;

AppendStructuredBuffer<GrassBlade> _GrassBlades;

uint _Resolution;
float _GrassSpacing;
float _JitterStrength;
float3 _PlaneCentre;

float _HeightMapScale;
float _HeightMapMultiplier;

float _FrustumCullNearOffset;
float _FrustumCullEdgeOffset;

float _DistanceCullStartDist;
float _DistanceCullEndDist;

float _DistanceCullMinimumGrassAmount;
float3 _WSpaceCameraPos;
float _WindTexContrast;

float _GlobalWindFacingAngle;
float _GlobalWindFacingContribution;

float _CentreColorSmoothStepLower;
float _CentreColorSmoothStepUpper;
float4x4 _VP_MATRIX;

Texture2D WindTex; 
SamplerState samplerWindTex;

float _GlobalWindFacing;

float _WindControl;
float _BigWindSpeed;
float _BigWindRotateAmount;
float _BigWindScale;
float _ClumpColorUniformity;

float4 _Time;

//float4 _ProjectionParams;

Texture2D HeightMap; 
SamplerState samplerHeightMap;

Texture2D ClumpTex; 
SamplerState my_point_repeat_sampler;
SamplerState samplerClumpTex;
float _ClumpScale;
float _NumClumpParameters;

Texture2D ClumpGradientMap; 
SamplerState samplerClumpGradientMap;

//Texture2D BladeGradientMap;
//SamplerState samplerBladeGradientMap;
//SamplerState my_linear_mirror_sample;



        float2 hashwithoutsine22(float2 p)
{
	float3 p3 = frac(float3(p.xyx) * float3(.1031, .1030, .0973));
    p3 += dot(p3, p3.yzx+33.33);
    return frac((p3.xx+p3.yz)*p3.zy);
}

    float rand(float3 co)
	        {
		        return frac(sin(dot(co.xyz, float3(12.9898, 78.233, 53.539))) * 43758.5453);
	        }


    float remap01_neg11(float t){
    
        return (t*2)-1;

    }

    uint DistanceCull(float3 worldPos, float hash, float3 posToCam){
    
       

        //float viewAngleModulation = pow(saturate(dot(norm, posToCam)  * _DistanceCullC), _Test);

        //float viewAngleCull =  hash > (1-viewAngleModulation) ? 1 : -1;


        //Distance culling
        float d = distance(worldPos, _WSpaceCameraPos);

        float distanceSmoothStep = 1-smoothstep(_DistanceCullStartDist, _DistanceCullEndDist, d);

        distanceSmoothStep = (distanceSmoothStep * (1-_DistanceCullMinimumGrassAmount)) + _DistanceCullMinimumGrassAmount;

        //distanceSmoothStep *= viewAngleModulation;

        //float combined = viewAngleCull * _DistanceCullM +(1-distanceSmoothStep);

        return hash > 1-distanceSmoothStep ? 1 : 0;


        //return saturate(viewAngleCull * _DistanceCullM + distanceCull);
        //return distanceCulling > _CullBoundary ? 1 : 0;

        
    }

    uint FrustumCull(float4 clipPos){
    return(clipPos.z > clipPos.w 
    || clipPos.z < - clipPos.w 
    || clipPos.x < - clipPos.w + _FrustumCullEdgeOffset
    || clipPos.x > clipPos.w - _FrustumCullEdgeOffset
    || clipPos.y < - clipPos.w +_FrustumCullNearOffset
    || clipPos.y > clipPos.w)
        ? 0: 1;
}

    float3 getTransformedVertex(float3 vertex){
    

        float2 worldUV = vertex.xz;
        
        worldUV = worldUV * (1/_HeightMapScale.xx);

        float height = HeightMap.SampleLevel(samplerHeightMap, worldUV, 0).x;

        vertex.y += height*_HeightMapMultiplier;

        return vertex;
    
    }


    float2 rot90CCW(float2 vec){
    
        return float2(-vec.y,vec.x);
    
    }


//  float4 ComputeScreenPos (float4 pos) {
//    float4 o = pos * 0.5f;
//    #if defined(UNITY_HALF_TEXEL_OFFSET)
//    o.xy = float2(o.x, o.y*_ProjectionParams.x) + o.w * _ScreenParams.zw;
//    #else
//    o.xy = float2(o.x, o.y*_ProjectionParams.x) + o.w;
//    #endif
 
//    o.zw = pos.zw;
//    return o;
//}

//     float BlendMode_Overlay(float base, float blend)
//{
//	return (base <= 0.5) ? 2*base*blend : 1 - 2*(1-base)*(1-blend);
//}

//float3 BlendMode_Overlay(float3 base, float3 blend)
//{
//	return float3(  BlendMode_Overlay(base.r, blend.r), 
//					BlendMode_Overlay(base.g, blend.g), 
//					BlendMode_Overlay(base.b, blend.b) );
//}

//float BlendMode_LinearDodge(float base, float blend)
//{
//	return min(1, base + blend);
//}

//float3 BlendMode_LinearDodge(float3 base, float3 blend)
//{
//	return float3(  BlendMode_LinearDodge(base.r, blend.r), 
//					BlendMode_LinearDodge(base.g, blend.g), 
//					BlendMode_LinearDodge(base.b, blend.b) );
//}

[numthreads(8,8,1)]
void Main (uint3 id : SV_DispatchThreadID)
{
    // TODO: insert actual code here!



    if (id.x < _Resolution && id.y < _Resolution){

        //Calculate xz position
        float3 position = float3(id.x,0,id.y)*_GrassSpacing - _PlaneCentre;
        
        float2 hash = hashwithoutsine22(id.xy);

        //Jitter xz
        float2 jitter = ((hash*2)-1 ) * _JitterStrength;

        position.xz += jitter;

        float2 clumpUV = position.xz * float2(_ClumpScale.xx);

        //Retrieve clump data for this blade from voronoi texture
        //This includes the clump parameter id, and the centre of this clump in texture space
        float3 clumpData = ClumpTex.SampleLevel(samplerClumpTex, clumpUV,0).xyz;

        //This is the index of the clump parameter set for this blade
        float clumpParamsIndex = clumpData.x;

        //Retrieve the correct set of blade parameters
        ClumpParametersStruct bladeParameters = _ClumpParameters[int((clumpParamsIndex))]; 

        //bladeParameters = _ClumpParameters[2];

        //Compute the clump centre in world space by finding its offsetted position in texture space and -dividing- that by the voronoi tiling 
        float2 clumpCentre = (clumpData.yz + floor(clumpUV))/float2(_ClumpScale.xx);

        //Pull position to centre of clump based on pullToCentre
        position.xz = lerp(position.xz, clumpCentre, bladeParameters.pullToCentre);


        float3 raisedVertex = getTransformedVertex(position);
        float3 posToCam =  normalize(_WSpaceCameraPos - raisedVertex);

        uint distanceCull = 1;
        //Distance culling
        #if DISTANCE_CULL_ENABLED

            distanceCull = DistanceCull(raisedVertex, hash.x, posToCam);
        #endif

        float2 worldUV = position.xz;

        float3 bitangent = float3(1, 0, 0);
        float3 tangent   = float3(0, 0, 1);

        //Get y height from heightmap
        // Also calc surface normal from height map. Probably better to read from a texture
        float3 vertexBitangent = getTransformedVertex(position + bitangent*0.01);
        float3 vertexTangent = getTransformedVertex(position + tangent*0.01);
        

        float3 newBitangent = (vertexBitangent-raisedVertex ).xyz;
        float3 newTangent = (vertexTangent-raisedVertex).xyz;

        float3 norm = normalize(cross(newTangent,newBitangent));



        
        //Get clipPos for easier frustum culling in clip space
        float4 clipPos = mul(_VP_MATRIX, float4(raisedVertex,1));

        //float4 screenPos = ComputeScreenPos(clipPos);
        
        //float2 screenUV = screenPos.xy / screenPos.w;

        //float depth =  CameraDepthTexture.SampleLevel(MyLinearClampSampler, screenUV, 0).x;
        //_DepthTexture[uv.xy];
        //float depth = DepthTexture[float2(0,0)];

        //float depth = 1;

        uint occlusionCull = 1;

        //Frustum culling
        uint isInFrustum = FrustumCull(clipPos);

        

        //isInFrustum = 1;
        //distanceCull = 1;
        occlusionCull = 1;


        if (isInFrustum == 1 && distanceCull == 1 && occlusionCull == 1){
            
            //Copy parameters from parameter struct
            //float4 clumpColor = bladeParameters.clumpColor;
            float baseHeight = bladeParameters.baseHeight;
            float heightRandom = bladeParameters.heightRandom;
            float baseWidth = bladeParameters.baseWidth;
            float widthRandom = bladeParameters.widthRandom;
            float baseTilt = bladeParameters.baseTilt;
            float tiltRandom = bladeParameters.tiltRandom;
            float baseBend = bladeParameters.baseBend;
            float bendRandom = bladeParameters.bendRandom;
            
            //Start building grassblade struct
            GrassBlade blade;



            //float2 positionXZ = lerp(clumpCentre, );

            blade.position = raisedVertex;
            

            

            //float2 bladeFacing = normalize(float2(rand(id.xxy),rand(id.yxx) ) *2 - 1); 

            //Compute the facing by lerping between random facing and shared clump facing based on the pointInSameDirection parameter

            float2 clumpHash = hashwithoutsine22(clumpCentre);

            //float2 clumpHash = float2(rand(clumpCentre.xyx), rand(clumpCentre.yyx));

            float2 sharedClumpFacing = normalize( tan((clumpHash+float2(0.13, 1.111))  *2 - 1));

            //float2 sharedClumpFacing = normalize(clumpCentre);

            float2 bladeFacing = normalize( hashwithoutsine22(id.xy)  *2 - 1); 

            float2 combinedFacing = normalize(lerp(bladeFacing, sharedClumpFacing, bladeParameters.pointInSameDirection));

            float2 globalWindFacingVec= float2(cos(_GlobalWindFacingAngle), sin(_GlobalWindFacingAngle));

            float2 globalFacing = normalize(lerp(combinedFacing, globalWindFacingVec, _GlobalWindFacingContribution));

            bladeFacing = globalFacing ;

            float angle = atan2(bladeFacing.y,bladeFacing.x);


           //float grassFacingAngle = atan2(bladeFacing.y,bladeFacing.x);


                //---Calculate wind
                

                float2 bigWindUV = worldUV * (_BigWindScale);

                bigWindUV += _Time.y * globalWindFacingVec *_BigWindSpeed;

                //Alternate method for big wind using same method as small wind
               

                float bigWind = WindTex.SampleLevel(samplerWindTex, bigWindUV, 0).x;

                float bigTheta = ((bigWind*2)-1)* 3.14159;

                float2 bigWindDir = (float2(cos(bigTheta), sin(bigTheta)));

                float2 grassSideVec = normalize(float2(-bladeFacing.y, bladeFacing.x));

                float rotateBladeFromBigWindAmount = dot(grassSideVec, bigWindDir); 

                _BigWindRotateAmount = lerp(0, _BigWindRotateAmount, _WindControl);

                float bigWindRotateAngle = rotateBladeFromBigWindAmount * (3.14159/2) * _BigWindRotateAmount;

                //float2 smallWindUV = worldUV * (_SmallWindScale);

                //smallWindUV += _Time.y * float2(1,0) *_SmallWindSpeed;

                //float smallWind = WindTex.SampleLevel(samplerWindTex, smallWindUV, 0).y;

                //float smallWindRotateAngle = (smallWind)* 3.14159 * _SmallWindRotateAmount;



                //float2 cameraDirXZ = posToCam.xz;

                //float cameraAngle = atan2(cameraDirXZ.y,cameraDirXZ.x);



                angle += bigWindRotateAngle;

                

            blade.rotAngle = angle;
            blade.hash = rand(id.xyx);



            blade.height = baseHeight + remap01_neg11(  rand(id.xxy) ) * heightRandom;
            blade.width = baseWidth + remap01_neg11(  rand(id.yxx) ) * widthRandom;
            //0-1 value, controlling the vertical component of the p3 point in the bezier curve, horizontal can be derived from pythag.
            blade.tilt = baseTilt + remap01_neg11(  rand(id.xyx * float3(1.12, 3.3, 17.6)) ) * tiltRandom;
            blade.bend = baseBend + remap01_neg11(  rand(id.xyy * float3(12.32, 0.23, 3.39)) ) * bendRandom;

            blade.surfaceNorm = norm;
            float distanceToCentre = distance(raisedVertex.xz,  clumpCentre);

            float atten = 1-smoothstep(_CentreColorSmoothStepLower,_CentreColorSmoothStepUpper , distanceToCentre);
            float3 clumpColor =  ClumpGradientMap.SampleLevel(samplerClumpGradientMap, clumpHash.x, 0).xyz;

            float3 bladeColor =  ClumpGradientMap.SampleLevel(samplerClumpGradientMap, blade.hash.x, 0).xyz;
            //float3 bladeColor =  BladeGradientMap.SampleLevel(samplerBladeGradientMap, blade.hash.x, 0).xyz;

            //float3 combinedColor = lerp(clumpColor, clumpColor*bladeColor, _ClumpColorUniformity);
            //float3 blendedColor = clumpColor*bladeColor;
            //float3 blendedColor = BlendMode_LinearDodge(clumpColor, bladeColor);

            float3 finalColor = lerp(clumpColor, bladeColor , _ClumpColorUniformity);

            blade.color = finalColor.xyz;
            
            float viewAlignment = abs(dot(bladeFacing, normalize(posToCam.xz)));
            float sideBend = smoothstep(0.3, 0, viewAlignment)*1.5;
            //float sideBend = viewAlignment < _Test  ? _Test2 : 0;
            

            blade.sideBend = sideBend;

            blade.clumpColorDistanceFade = atten;
            float windForce = saturate(  ((bigWind - 0.5) * max(_WindTexContrast, 0)) + 0.5f   );
            blade.windForce = (windForce);

            _GrassBlades.Append(blade);

        }

        
    }

}
